            ;ACME 0.97
            !cpu 6510

            !src <lib/registers/pseudo-registers.a>
            !src <lib/mmap/main.a>


; Copy the character set from ROM to RAM.
;
; Character set at $d000 will be copied to the page
; specified in Y.
;
; IN:   Y = destination page MSB
; USE:  A X Y R1 R2
copy_charset_to_ram:
            sei

            ; Make the CPU see the character ROM
            lda R6510
            sta .r6510
            and #%11111011
            sta R6510

            ; Copy the character set
            lda #8
            ldx #>CHARROM
            jsr copy_mem_pages

            ; Restore original I/O register value
            lda .r6510
            sta R6510

            cli

            rts

.r6510      !byte 0


            !src <lib/mem/copy-mem-pages.a>
