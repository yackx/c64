; A book reader.
;
; See README.md for more information and instructions on how to
; build the book disk image.

            ;ACME 0.97
            !cpu 6510

            !addr SCREEN_MEM = DEFAULT_SCREEN_MEM

            !src <lib/misc/upstart.a>
            !src <lib/registers/pseudo-registers.a>
            !src <lib/screen/color.a>
            !src <lib/layout/main.a>
            !src <lib/layout/kernal.a>
            !src <lib/charset/petscii.a>
            !src <lib/charset/m-charset.a>
            !src <lib/screen/m-screen.a>
            !src <lib/strings/m-strout.a>


main:
            jsr CINT
            jsr clear_screen
            jsr screen_intro
            jsr read_info


load_page:
            jsr clear_screen
            jsr read_page
            jsr footer

.key        jsr keypress

            cmp #PETSCII_F5
            beq next_page

            cmp #PETSCII_F3
            beq prev_page

            cmp #PETSCII_F1
            bne +
            jsr screen_intro
            jmp load_page

+           cmp #'G'
            bne +
            jsr display_goto
            jmp load_page

+           cmp #'C'
            bne +
            jsr screen_color_theme
            jmp load_page

+           cmp #'Q'
            bne .key
            rts

next_page:  lda curr_page
            cmp max_page
            beq .key
            inc curr_page
            jmp +

prev_page:  lda curr_page
            beq .key
            dec curr_page

+           jsr on_change_curr_page
            jmp load_page


read_page:
            lda #PETSCII_REVOFF
            jsr CHROUT
            
            lda #<page_filename
            sta read_file_param_filename_addr
            lda #>page_filename
            sta read_file_param_filename_addr+1
            lda #page_filename_end - page_filename
            sta read_file_param_filename_len

            lda #<read_page_chr_vect
            sta read_file_param_char_vect
            lda #>read_page_chr_vect
            sta read_file_param_char_vect+1

            jsr read_file

            rts


read_page_chr_vect:
            jsr CHROUT

            rts


footer:
            lda theme_color_muted
            jsr display_color_bar

            +set_cursor_at 0, 24

            lda curr_page
            jsr print_int_8

            lda #'/'
            jsr CHROUT

            lda max_page
            jsr print_int_8

            rts


clear_screen:
            lda #PETSCII_CLR
            jsr CHROUT

            +switch_lower_case
            
            lda theme_color_border
            sta VIC2BORDER
            lda theme_color_background
            sta VIC2BACKGROUND

            lda theme_color_primary
            jsr chrout_petscii_to_color
            
            rts


on_change_curr_page:
            lda curr_page
            jsr convert_hex
            lda convert_hex_buffer
            sta hex_page
            lda convert_hex_buffer+1
            sta hex_page+1
            rts


curr_page:  !byte 1
max_page:   !byte 39

page_filename:
            !pet "page"
hex_page:   !pet "01"
            !pet ",u"                       ; "PAGExx.USR" on disk
page_filename_end:

            !src <lib/mem/fill-pages.a>
            !src <lib/baseconv/convert-hex.a>
            !src <lib/baseconv/print-int-8.a>
            !src <lib/io/keyb/keypress.a>
            !src <lib/strings/strout.a>

            !src <programs/book-reader/utils.a>
            !src <programs/book-reader/intro.a>
            !src <programs/book-reader/color-theme.a>
            !src <programs/book-reader/footer-bar.a>
            !src <programs/book-reader/file-reader.a>
            !src <programs/book-reader/page-flipper.a>
            !src <programs/book-reader/read-info.a>
            !src <lib/screen/scr-clr-petscii.a>
