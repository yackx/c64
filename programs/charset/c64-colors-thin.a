; Etude - Commodore badge lines (5 colors)
;
; Text multicolor mode: 4 colors / char
; $d021 - background color (shared by all chars)
; $d022 - shared color #1
; $d023 - shared color #2
; unused - character own color in Color RAM
;
; Custom character set of 2 chars:
; @ - double stripe (2 pixels stripe / 2 pixels blank)
; A - single stripe (2 pixels stripe)
;
; Layout:
; line 1 - @
; line 2 - @ (same char, we change the shared colors)
; line 3 - A
;
; For a total of 5 stripes taken from the C64 badge.
;
; We use 3 raster IRQs to change the shared colors.

                ;ACME 0.97
                !cpu 6510

                !src <lib/misc/upstart.a>
                !src <lib/registers/pseudo-registers.a>
                !src <lib/mem/m-mem.a>
                !src <lib/raster/m-raster.a>

                ; Charset location
                !addr CHAR_SET_LOC = $3800
                ; Mask for setting charset (depends on CHAR_SET_LOC)
                CHAR_PTR_BITMASK = $0e

                RASTER_LINE_1 = 50
                RASTER_LINE_2 = 50 + 8
                RASTER_LINE_3 = 50 + 8*2

                BG_COLOR = 0                


main:
                ; Clear screen
                jsr $ff81

                ; Blank charset memory
                ; Most characters are blank
                +fill_pages >CHAR_SET_LOC, 8, 0

                ; Point to custom charset
                lda #CHAR_PTR_BITMASK
                jsr set_charset_loc

                ; Multicolor mode
                lda $d016
                ora #%00010000
                sta $d016

                ; Copy custom characters
                +copy_bytes_8 double_stripe, CHAR_SET_LOC, 8*2

                ; Background color
                lda #BG_COLOR
                sta $d020
                sta $d021

                ; Print lines
                lda #'@'
                jsr print_line
                jsr print_line
                lda #'A'
                jsr print_line

                ; Enable raster IRQ
                sei
                lda #%01111111
                sta $dc0d
                and $d011
                sta $d011
                +set_raster_irq RASTER_LINE_1, irq_handler_1
                lda #%00000001
                sta $d01a
                cli

                jmp *


irq_handler_1:
                ;+set_border_color 1
                +set_raster_irq RASTER_LINE_2, irq_handler_2

                lda stripe_colors
                sta $d022
                lda stripe_colors+1
                sta $d023

                ;+set_border_color 0
                +ack_short_kernal


irq_handler_2:
                ;+set_border_color 1
                +set_raster_irq RASTER_LINE_3, irq_handler_3

                lda stripe_colors+2
                sta $d022
                lda stripe_colors+3
                sta $d023

                ;+set_border_color 0
                +ack_short_kernal


irq_handler_3:
                ;+set_border_color 1
                +set_raster_irq RASTER_LINE_1, irq_handler_1

                lda stripe_colors+4
                sta $d022

                ;+set_border_color 0
                +ack_short_kernal


; Print a line of the character stored in reg A
print_line:
                ldx #40                 ; column
-
                jsr $ffd2
                dex
                bne -

                rts


; Charset variant: 2 pixels stripe / 2 pixels blank
double_stripe:  !byte 85,85,0,0,170,170,0,0
single_stripe:  !byte 85,85,0,0,0,0,0,0

stripe_colors:  !byte 2, 8
                !byte 7, 5
                !byte 14

                !src <lib/mem/m-mem.a>
                !src <lib/charset/reloc-charset.a>
