; Etude - Commodore badge lines (5 colors)
;
; Text multicolor mode: 4 colors / char
; $d021 - background color (shared by all chars)
; $d022 - shared color #1
; $d023 - shared color #2
; unused - character own color in Color RAM
;
; Custom character set of 2 chars:
; @ - double stripe (3 pixels stripe / 3 pixels blank / 2 pixels stripe)
; A - single stripe (1 stripe / 3 blank / 3 stripe / 1 blank)
; B - single stripe (2 blank / 3 stripe)
;
; Layout:
; line 1 - @
; line 2 - A
; line 3 - B
;
; For a total of 5 stripes taken from the C64 badge.
;
; We use 3 raster IRQs to change the shared colors.

                                            ;ACME 0.97
            !cpu 6510

            !src <lib/misc/upstart.acme>
            !src <lib/registers/pseudo-registers.acme>
            !src <lib/mmap/basic-vector.acme>
            !src <lib/mem/m-mem.acme>
            !src <lib/raster/m-raster.acme>

                                            ; Charset location
            !addr CHAR_SET_LOC = $3800

                                            ; Mask for setting charset (depends on CHAR_SET_LOC)
            CHAR_PTR_BITMASK = $0e

            RASTER_OFFSET = 12
            RASTER_LINE_1 = 50
            RASTER_LINE_2 = RASTER_LINE_1 + RASTER_OFFSET
            RASTER_LINE_3 = RASTER_LINE_1 + RASTER_OFFSET*2

            BG_COLOR = 0


// @TODO Delete me
!macro set_border_color @color {
            lda #@color
            sta $d020
}


main:
                                            ; Clear screen
            jsr $ff81

                                            ; Blank charset memory
                                            ; Most characters are blank
            +fill_pages >CHAR_SET_LOC, 8, 0

                                            ; Point to custom charset
            lda #CHAR_PTR_BITMASK
            jsr set_charset_loc

                                            ; Multicolor mode
            lda $d016
            ora #%00010000
            sta $d016

                                            ; Copy custom characters
            +copy_bytes_8 charset, CHAR_SET_LOC, 8*NB_OF_CHARS

                                            ; Background color
            lda #BG_COLOR
            sta $d020
            sta $d021

                                            ; Print lines
            lda #$40
            jsr print_line
            lda #$41
            jsr print_line
            lda #$42
            jsr print_line
            lda #$43
            jsr print_line
            lda #$44
            jsr print_line

                                            ; Enable raster IRQ
            sei
            lda #%01111111
            sta $dc0d
            and $d011
            sta $d011
            +set_raster_irq RASTER_LINE_1, irq_handler_1
            lda #%00000001
            sta $d01a
            cli

            jmp *


irq_handler_1:
                                            ;+set_border_color 1
            +set_raster_irq RASTER_LINE_2, irq_handler_2

            lda stripe_colors
            sta $d022
            lda stripe_colors+1
            sta $d023

                                            ;+set_border_color 0
            +ack_short_kernal


irq_handler_2:
                                            ;+set_border_color 1
            +set_raster_irq RASTER_LINE_3, irq_handler_3

            lda stripe_colors+2
            sta $d022
            lda stripe_colors+3
            sta $d023

                                            ;+set_border_color 0
            +ack_short_kernal


irq_handler_3:
                                            ;+set_border_color 1
            +set_raster_irq RASTER_LINE_1, irq_handler_1

            lda stripe_colors+4
            sta $d022
            sta $d023

            +set_border_color 0
            +ack_short_kernal


; Print a line of the character stored in reg A
print_line:
            ldx #40                         ; column
-
            jsr $ffd2
            dex
            bne -

            rts


; Charset variant: 3 pixels stripe / 3 pixels blank
charset:
stripes_1:  !byte 85,85,85,0,0,0,170,170
stripes_2:  !byte 170,0,0,0,85,85,85,0
stripes_3:  !byte 0,0,170,170,170,0,0,0
stripes_4:  !byte 85,85,85,0,0,0,0,0

            NB_OF_CHARS = (* - charset) / 8

stripe_colors:!byte 2, 8
            !byte 7, 5
            !byte 14

            !src <lib/charset/reloc-charset.acme>
