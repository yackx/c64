; Print multiple messages directly to screen memory.
;
; This is faster than using the KERNAL routine CHROUT,
; and is not necessarily more cumbersome as we avoid
; manipulating the registers as we would 
; to set the cursor position if we used CHROUT.

            ;ACME 0.97
            !cpu 6510

            !src <common/upstart.a>
            !src <common/pseudo-reg.a>
            !src <common/registers.a>
            !src <common/kernal.a>


main:   
            jsr CINT           
            lda #$0e                    ; Lower case font
            jsr CHROUT
            lda #$08                    ; Font case toggle disabled
            jsr CHROUT

            ldx #0                      ; idx text_scr
            cld
.print_msg:
            ldy #0                      ; idx char in text
            lda text_scr,x
            sta R1
            lda text_scr+1,x
            sta R2
.print_char:
            lda text,y                  ; self-mod address
            cmp #EOT
            beq .next_msg
            sta (R1),y
            iny
            bne .print_char
.next_msg:
            iny
            tya
            clc
            adc .print_char+1
            sta .print_char+1
            bcc +
            inc .print_char+2
+            
            inx
            inx
            cpx #TEXT_COUNT * 2
            bne .print_msg

            rts

            TEXT_COUNT = 6
            EOT = $ff

text:
.text1      !scr "Multiple text messages", EOT
.text2      !scr "printed directly to screen memory.", EOT
.text3      !scr "Faster than CHROUT, and not necessarily", EOT
.text4      !scr "less readable.", EOT
.text5      !scr "https://github.com/yackx/c64", EOT
.text6      !scr "Youri Ackx (C) 2023-2024", EOT
.text7

            SCR = $0400
text_scr:   !word SCR + 40*5 + (41-.text2+.text1)/2
            !word SCR + 40*6 + (41-.text3+.text2)/2
            !word SCR + 40*8 + (41-.text4+.text3)/2
            !word SCR + 40*9 + (41-.text5+.text4)/2
            !word SCR + 40*12 + (41-.text6+.text5)/2
            !word SCR + 40*14 + (41-.text7+.text6)/2
