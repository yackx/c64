            ;ACME 0.97
            !cpu 6510

            !src <lib/registers/pseudo-registers.acme>


; Multiply A by 10.
;
; The multiplication is done by shifting A left twice,
; adding A to the result, and then shifting the result once more.
;
; IN:       A
; OUT:      R3 R4
mul_10:
            pha
            sta R3                          ; Store A
            lda #0
            sta R4
            pla

            asl
            rol R4                          ; A * 2
            
            asl
            rol R4                          ; A * 4

            clc
            adc R3
            bcc +
            inc R4                          ; A * 5

+           asl
            rol R4                          ; A * 10

            sta R3

            rts
