            ;ACME 0.97
            !cpu 6510


; Copy memory pages.
;
; IN:       X = source page MSB
;           Y = destination page MSB
;           A = number of pages to copy
; USES:     A, X, Y
;
; Example:
;
;   X=$04, Y=$06, A=2
;   jsr copy_mem_pages
;
; will copy 2 pages $0400-$05FF to $0600$07FF.
copy_mem_pages:
            stx .src
            sty .dest
            ldy #0                          ; Index inside page
            tax                             ; Number of pages to copy
-
            .src = *+2                      ; Self modifying
            lda $0000,y

            .dest = *+2                     ; Self modifying
            sta $0000,y

            iny
            bne -
            dex
            beq +
            inc .src                        ; Next source page
            inc .dest                       ; Next destination page
            jmp -

+           rts
