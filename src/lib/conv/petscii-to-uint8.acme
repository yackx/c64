; Convert a decimal value in PETSCII to a byte.
;
; Example: user enters the digits 1, 2 and 3.
; The PETSCII values in petscii_to_uint8_params
; are 49, 50, 51 and X = 3 (how many).
; After invoking this routine, A = 123.
;
; IN:       petscii_to_uint8_params (max 3 chars)
;           X = how many digits (0-3)
; OUT:      A
; USES:     R3 R4


            ;ACME 0.97
            !cpu 6510


!zone zone_petscii_to_uint8 {

petscii_to_uint8_params:
            !fill 3, 0

petscii_to_uint8:
            stx .len
            ldx #0
            stx .value

.loop       cpx .len
            beq .done
            lda .value
            jsr mul_10

            lda R4
            bne .invalid

            lda petscii_to_uint8_params,x
            sec
            sbc #'0'
            clc
            adc R3
            sta .value
            bcs .invalid

            inx
            jmp .loop

.done:      lda .value
            clc
            rts

.invalid:   sec
            rts


.len:       !byte 0
.value:     !byte 0
}


            !src <lib/calc/mul-10.acme>