; Etude - Commodore badge lines (5 colors)
;
; Text multicolor mode: 4 colors / char
; $d021 - background color (shared by all chars)
; BGCOL1 - shared color #1
; BGCOL2 - shared color #2
; unused - character own color in Color RAM
;
; Custom character set of 4 chars:
; @ - double stripe (3 pixels stripe / 3 pixels blank / 2 pixels stripe)
; A - single stripe (1 stripe / 3 blank / 3 stripe / 1 blank)
; B - single stripe (2 blank / 3 stripe / 3 blanks)
; C - single stripe (3 stripe / 4 blank)
;
; Layout:
; line 1 - @
; line 2 - A
; line 3 - B
; line 4 - C
;
; For a total of 5 stripes taken from the C64 badge.
;
; We use 3 raster IRQs to change the shared colors.

            ;ACME 0.97
            !cpu 6510

            !src <lib/mmap/zp_free.acme>
            !src <lib/mmap/basic_vector.acme>
            !src <lib/mmap/kernal.acme>
            !src <lib/mmap/cia.acme>
            !src <lib/mem/m_mem.acme>
            !src <lib/screen/color.acme>
            !src <lib/raster/m_raster.acme>
            !src <lib/misc/upstart.acme>

            ; Charset location
            !addr CHAR_SET_LOC = $3800

            ; Mask for setting charset (depends on CHAR_SET_LOC)
            CHAR_PTR_BITMASK = $0e

            RASTER_OFFSET = 12
            RASTER_LINE_1 = 50
            RASTER_LINE_2 = RASTER_LINE_1 + RASTER_OFFSET
            RASTER_LINE_3 = RASTER_LINE_1 + RASTER_OFFSET*2


main:
            ; Clear screen
            jsr CINT

            ; Blank charset memory - most characters are blank
            +fill_pages >CHAR_SET_LOC, 8, 0

            ; Point to custom charset
            lda #CHAR_PTR_BITMASK
            jsr set_charset_loc

            ; Multicolor mode
            lda VIC2_CR2
            ora #%00010000
            sta VIC2_CR2

            ; Copy custom characters
            +copy_bytes_8 charset, CHAR_SET_LOC, 8*NB_OF_CHARS

            ; Background color
            lda #COLOR_BLACK
            sta BORDER_COLOR
            sta BACKGROUND_COLOR

            ; Print lines
            lda #'@'
            jsr print_line
            lda #'A'
            jsr print_line
            lda #'B'
            jsr print_line
            lda #'C'
            jsr print_line

            ; Enable raster IRQ
            sei
            lda #%01111111
            sta CIAICR
            and VIC2_CR1
            sta VIC2_CR1
            +set_raster_irq RASTER_LINE_1, irq_handler_1
            lda #%00000001
            sta VIC2_IRQ_MASK
            cli

            jmp *


irq_handler_1:
            +set_raster_irq RASTER_LINE_2, irq_handler_2

            lda stripe_colors
            sta BGCOL1
            lda stripe_colors+1
            sta BGCOL2

            +ack_short_kernal


irq_handler_2:
            +set_raster_irq RASTER_LINE_3, irq_handler_3

            lda stripe_colors+2
            sta BGCOL1
            lda stripe_colors+3
            sta BGCOL2

            +ack_short_kernal


irq_handler_3:
            +set_raster_irq RASTER_LINE_1, irq_handler_1

            lda stripe_colors+4
            sta BGCOL1
            sta BGCOL2

            +ack_short_kernal


; Print a line of the character stored in reg A
print_line:
            ldx #40                         ; column
-
            jsr CHROUT
            dex
            bne -

            rts


; Charset pattern: 3 pixels stripe followed 3 pixels blank
charset:
            !byte 85,85,85,0,0,0,170,170
            !byte 170,0,0,0,85,85,85,0
            !byte 0,0,170,170,170,0,0,0
            !byte 85,85,85,0,0,0,0,0

            NB_OF_CHARS = (* - charset) / 8

stripe_colors:
            !byte 2, 8, 7, 5, 14

            !src <lib/charset/reloc_charset.acme>
