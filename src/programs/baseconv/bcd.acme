; Binary / BCD conversion

            ;ACME 0.97
            !cpu 6510

            !src <lib/mmap/zp-free.acme>
            !src <lib/mmap/kernal.acme>
            !src <lib/misc/upstart.acme>


main:
            lda #234                        ; example binary value
            jsr byte_to_bcd
            jsr print_bcd
            rts


; Convert an 8 bit binary value to BCD.
;
; Andrew Jacobs, 28-Feb-2004
; https://codebase64.org/doku.php?id=base:more_hexadecimal_to_decimal_conversion
;
; IN        A
; OUT       R3 R3+1
byte_to_bcd:
            sta .binary
            sed
            lda #0
            sta R3
            sta R3+1
            ldx #8                          ; number of bits
-           asl .binary
            lda R3+0
            adc R3+0
            sta R3+0
            lda R3+1
            adc R3+1
            sta R3+1
            dex
            bne -
            cld

            lda R3+0
            ldy R3+1

            rts

.binary:    !byte 0


; Print BCD
;
; ex: binary 234 => bcd $34 $02.
; Converting a value to PETSCII requires adding $30.
;
; IN        R3 R3+1
print_bcd:
            PETSCII_OFFSET = $30

            lda R3+1                        ; $02 => "2"
            clc
            adc #PETSCII_OFFSET
            jsr CHROUT

            lda R3+0                        ; $34 => "3"
            and #%11110000                  ; $34 = %00110100 => %00110000
            ror                             ; shift right 4 bits
            ror
            ror
            ror
            clc
            adc #PETSCII_OFFSET
            jsr CHROUT

            lda R3+0                        ; $34 => "4"
            and #%00001111                  ; $34 = %00110100 => %00000100
            clc
            adc #PETSCII_OFFSET
            jsr CHROUT

            rts
