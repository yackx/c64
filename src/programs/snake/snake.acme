; Boring Snake
;
; Good old snake game. Simple. Boring.

            ;ACME 0.97
            !cpu 6510

            !src <lib/mmap/mmap.acme>
            !src <lib/screen/color.acme>
            !src <lib/screen/codes.acme>
            !src <lib/charset/petscii.acme>
            !src <lib/registers/m_registers.acme>
            !src <lib/strings/m_print_color_msg_xy.acme>
            !src <lib/io/keyb/m_keypress.acme>

            !src <lib/misc/upstart.acme>


            !addr SCREEN_ADDR = DEFAULT_SCREEN_MEM

            DELAY = 15                      ; 1/60th sec
            INIT_LENGTH = 4
            GROW = 4

            ; Snake head or body part position
            POS_X       = R5
            POS_Y       = R6

            ; Screen characters
            CHAR_HEAD   = SCR_CODE_CIRCLE_INV
            CHAR_BODY   = SCR_CODE_CIRCLE
            CHAR_BLANK  = SCR_CODE_WHITE_SPACE
            CHAR_FOOD   = SCR_CODE_HEART

            ; Snake direction
            LEFT        = %00000001
            RIGHT       = %00000010
            UP          = %00000100
            DOWN        = %00001000

            ; Keys
            KEY_UP      = PETSCII_CURSOR_UP
            KEY_DOWN    = PETSCII_CURSOR_DOWN
            KEY_LEFT    = PETSCII_CURSOR_LEFT
            KEY_RIGHT   = PETSCII_CURSOR_RIGHT


; ===========================================================================
; SCREEN - INTRO
; ===========================================================================

; Intro screen.
;
; Wait for space to start.
; This allows to introduce a variable delay used
; to seed the random number generator.
intro_scr:
            jsr CINT

            ; Messages
            +m_print_color_msg_struct_at s_title
            +m_print_color_msg_struct_at s_info1
            +m_print_color_msg_struct_at s_info2
            +m_print_color_msg_struct_at s_start

            ; Press space to start
            +m_keypress ' '

            ; Seed random number generator
            jsr init_rnd_sid

            ; Uppercase
            lda PETSCII_UPPERCASE
            jsr CHROUT

            jmp main_scr


s_title:    !byte 8, 8, COLOR_WHITE
            !byte 14                        ; lower case
            !pet "B o r i n g   S N A K E", 0
s_info1:    !byte 17, 5, COLOR_LBLUE
            !pet "ASM code available on GitHub", 0
s_info2:    !byte 18, 5, COLOR_LBLUE
            !pet "https://github.com/yackx/c64", 0
s_start:    !byte 22, 9, COLOR_LGREEN
            !pet "Press space to start", 0

            !src <lib/strings/print_color_msg_xy.acme>


; ===========================================================================
; SCREEN - MAIN
; ===========================================================================

; Main screen.
;
; Prepare the screen and start the game.
; A timer is set to handle the game loop.
main_scr:
            jsr CINT
            jsr init_snake
            jsr gen_food
            jsr display_food
            jsr init_kb
            jsr set_timer
            jmp *

            !src <lib/random/rnd_sid.acme>


; ---------------------------------------------------------------------------
; INIT ROUTINES
; ---------------------------------------------------------------------------

; Initialize game state
init_snake:
            lda #12
            sta head_x
            lda #5
            sta head_y

            lda #LEFT
            sta direction

            lda #INIT_LENGTH
            sta max_idx

            lda #12
            sta snake_x
            lda #13
            sta snake_x+1
            lda #14
            sta snake_x+2
            lda #15
            sta snake_x+3
            lda #16
            sta snake_x+4
            lda #5
            sta snake_y
            sta snake_y+1
            sta snake_y+2
            sta snake_y+3
            sta snake_y+4

            rts


; Set timer vector
set_timer:
            sei
            lda #<timer_handler
            sta CINV
            lda #>timer_handler
            sta CINV+1
            cli
            rts


; Initialize keyboard
init_kb:
            ; keyboard buffer size
            lda #1
            sta XMAX

            ; No key repeat
            lda #$40
            sta RPTFLA

            rts


; ---------------------------------------------------------------------------
; IRQ HANDLER
; ---------------------------------------------------------------------------

; CIA Timer A interrupt handler
;
; Acts as the main game loop.
timer_handler:
            ; Handler cursor on every irq for smooth movement
            jsr handle_cursor

            ; Perform game logic every n interrupts
            dec delay
            bne +                           ; skip frame

            ; Actual game frame - reset delay
            lda #DELAY
            sta delay

            ; Game logic
            jsr move_head
            jsr erase_tail
            jsr update_head
            jsr display_food
+
            jmp $ea31                       ; KERNAL std routine


; ---------------------------------------------------------------------------
; GAME ROUTINES
; ---------------------------------------------------------------------------

; Handle keyboard input to change snake direction.
handle_cursor:
            jsr SCNKEY
            jsr GETIN
            beq +                           ; no key pressed

            ldx #0
.find_key:  cmp key_idx,x                   ; find cursor key in table
            beq .key_found
            inx
            cpx #4
            beq handle_cursor               ; not a cursor key
            bne .find_key                   ; try next cursor key
.key_found: lda dir_idx,x                   ; find corresponding direction
            sta direction                   ; store it
+
            rts


; Game over - screen border goes red
game_over:
            ; Reset CIA timer A
            sei
            lda #$31
            sta $314
            lda #$ea
            sta $315
            cli

            ; Set screen border to red
            lda #2
            sta $d020

            ; Wait for keypress
            +m_keypress_any

            ; Reset screen border to light blue
            lda #14
            sta $d020

            jmp main_scr


; Move and display the head
;
; Collision detection with border and self takes place here.
move_head:
            lda direction

            ; Successive right shifts on direction
            ; will eventually set the carry flag
            ; as it contains exactly one bit set to 1.
            ; Adjust head position accordingly.
.move_l     lsr
            bcc .move_r
            dec head_x
            bmi game_over
            jmp +

.move_r     lsr
            bcc .move_u
            inc head_x
            ldx head_x
            cpx #40
            beq game_over
            jmp +

.move_u     lsr
            bcc .move_d
            dec head_y
            bmi game_over
            jmp +

.move_d     inc head_y
            ldx head_y
            cpx #25
            beq game_over
+
            lda head_x
            sta POS_X
            lda head_y
            sta POS_Y

.check_food_collision:
            jsr char_at                     ; A = char at (POS_X, POS_Y)
            cmp #CHAR_FOOD
            bne .check_body_collision
            ; Food eaten
            lda #CHAR_BLANK
            sta char
            jsr display_char
            ; Let the snake grow
            lda #GROW
            sta skip_tail
            ; Generate new food
            jsr gen_food
            jmp .display_head

.check_body_collision:
            cmp #CHAR_BLANK
            bne game_over

.display_head:
            lda #CHAR_HEAD
            sta char
            jsr display_char

            rts


; Erase the last bit of the tail
erase_tail:
            ldx max_idx                     ; Last index of snake

            ; Skip erase when growing
            lda skip_tail
            beq .erase
            dec skip_tail

            inc max_idx
            beq .max_length_reached

            inx                             ; X = max_idx
            jmp .draw

.max_length_reached:
            dec max_idx
            jmp .draw

.erase:
            lda snake_x,x
            sta POS_X
            lda snake_y,x
            sta POS_Y
            lda #CHAR_BLANK
            sta char
            jsr display_char
.draw
            ; We will shift each xy of the snake one position
            ; to the right -> loop max_idx-1 times
            dex

            lda #CHAR_BODY
            sta char
-
            lda snake_x,x
            sta snake_x+1,x                 ; snake_x+1 <- snake_x
            sta POS_X
            lda snake_y,x
            sta snake_y+1,x                 ; snake_y+1 <- snake_y
            sta POS_Y
            jsr display_char
            dex
            cpx #$ff
            bne -

            rts


; Update the snake head (first element of the snake body)
update_head:
            lda head_x
            sta snake_x
            lda head_y
            sta snake_y

            rts


; Display the char in A at (POS_X, POS_Y)
;
; IN:       A = char
;           POS_X = x position
;           POS_Y = y position
; USES:     R1, R2, Y
display_char:
            ; Select and store start of screen row in R1+R2
            ldy POS_Y
            lda row_lsb,y
            sta R1
            lda row_msb,y
            sta R2

            ; Display character in screen RAM
            lda char
            ldy POS_X
            sta (R1),y

            rts


; Get the char at (POS_X, POS_Y)
;
; IN:       POS_X = x position
;           POS_Y = y position
; OUT:      A = char
; USES:     R1, R2, A, Y
char_at:
            ; Select and store start of screen row in R1+R2
            ldy POS_Y
            lda row_lsb,y
            sta R1
            lda row_msb,y
            sta R2

            ; Load character from screen RAM
            ldy POS_X
            lda (R1),y

            rts


; Generate food at a random position
;
; USES:     A, Y
gen_food:
            ; Pick a random address
            lda $d41b                       ; random number
            and #%00001111                  ; 32 possible addresses
            asl                             ; *2 for 2 bytes per address
            tay

            ; Load random screen address
            ; and it as food position (will be displayed later)
            lda random_food_addr,y
            sta food
            lda random_food_addr+1,y
            sta food+1

            rts


; Display food
;
; The food is displayed after the snake head and body
; to avoid the food being erased.
; A flickering is visible if the food is on the snake's path
; as the head or body are overwritten.
display_food:
            pha
            lda food
            sta .food_addr
            lda food+1
            sta .food_addr+1
            lda #CHAR_FOOD
            .food_addr = *+1
            sta $0000                       ; self-modifying code
            pla
            rts


; ---------------------------------------------------------------------------
; DATA
; ---------------------------------------------------------------------------

; Delay between 2 game frames
delay       !byte 1

; To compute the x,y position of the head,
; and to avoid slow arithmetics, we declare 2 index tables
; representing the first col of each row on the screen:
; SCREEN_ADDR, SCREEN_ADDR+40, SCREEN_ADDR+80, etc.
;
; We split LSB and MSB to use ZP index.
; $0400, $0428, $0450 become:
; row_lsb = 00 28 50 ... 18 ...
; row_msb = 04 04 04 ... 05 ...
row_lsb:    !for .i, 0, 24 {
            !byte <(SCREEN_ADDR + .i*40)
            }

row_msb:    !for .i, 0, 24 {
            !byte >(SCREEN_ADDR + .i*40)
            }

; Keyboard / direction mapping
key_idx:    !byte KEY_UP, KEY_DOWN, KEY_LEFT, KEY_RIGHT
dir_idx:    !byte UP, DOWN, LEFT, RIGHT

; Character to print
char:       !byte CHAR_HEAD

; Food position
food:       !word 0

; Head is temporarily stored.
; It will be copied to the first byte of the snake body (x and y)
head_x:     !fill 1
head_y:     !fill 1

; Snake direction
direction:  !fill 1

; Skip tail when growing
skip_tail:  !byte 0

; Maximum index of the snake body
max_idx:    !fill 1

; Screen addresses where food can be generated.
; Avoid the screen borders.
; Generated with a Python script.
random_food_addr:
            !byte $2f, $05, $d6, $05, $19, $06, $8e, $07
            !byte $d7, $05, $ae, $06, $5f, $07, $bb, $04
            !byte $b7, $04, $da, $04, $c9, $06, $fa, $06
            !byte $1f, $06, $9e, $07, $2d, $05, $8e, $06
            !byte $bf, $06, $50, $07, $15, $07, $9b, $06
            !byte $53, $04, $34, $07, $d0, $04, $7d, $06
            !byte $9e, $04, $5d, $06, $06, $07, $72, $06
            !byte $a4, $04, $02, $05, $f2, $06, $9d, $06


; Snake body
            !align $ff, 0, 0
snake_x:
            * = snake_x + 256
snake_y:
