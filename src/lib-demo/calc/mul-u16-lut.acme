; Demo for 8-bit unsigned bytes multiplication with lookup tables.
;
; This programs performs a series of multiplications and checks the results.
;
; If the results are correct, it prints "ok" and exits.
;
; Otherwise, the screen border turns red and the following values are printed:
; - the current test index Y
; - factor A
; - factor X
; - expected low byte
; - expected high byte

            ;ACME 0.97
            !cpu 6510

            !src <lib/mmap/zp-free.acme>
            !src <lib/mmap/main.acme>
            !src <lib/mmap/vic-ii.acme>
            !src <lib/mmap/kernal.acme>
            !src <lib/screen/color.acme>
            !src <lib/strings/m-print.acme>
            !src <lib/strings/m-strout.acme>
            !src <lib/misc/upstart.acme>


main:
            ldy #0
-
            lda fact_a,y
            ldx fact_x,y
            jsr mul_u16_lut
            lda exp_lo,y
            cmp R3                          ; Product low byte
            bne fail
            lda exp_hi,y
            cmp R4                          ; Product high byte
            bne fail
            iny
            cpy #HOW_MANY
            bne -
            
            +m_strout msg_ok
            
            rts

fail:
            tya
            jsr print_hex
            +new_line

            lda fact_a,y
            jsr print_hex
            +new_line

            lda fact_x,y
            jsr print_hex
            +new_line

            lda exp_lo,y
            jsr print_hex
            +new_line

            lda exp_hi,y
            jsr print_hex
            +new_line

            lda #COLOR_RED
            sta BORDER_COLOR
            
            rts


; Test data.
; For example, Y=14 : A=2, X=255 expected result=510.
fact_a:     !byte 0, 1, 2, 255,     0, 1, 2, 255
            !byte 0, 1, 2, 255,     0, 1, 2, 255
fact_x:     !byte 0, 0, 0, 0,       1, 1, 1, 1
            !byte 2, 2, 2, 2,       255, 255, 255, 255
exp_lo:     !byte 0, 0, 0, 0,       0, 1, 2, 255
            !byte 0, 2, 4, <510,    0, 255, <510, <65025
exp_hi:     !byte 0, 0, 0, 0,       0, 0, 0, 0
            !byte 0, 0, 0, >510,    0, 0, >510, >65025

            HOW_MANY = (* - exp_hi)

msg_ok:     !pet "ok", 0


            !src <lib/calc/mul-u16-lut.acme>
            !src <lib/baseconv/print-hex.acme>
            !src <lib/strings/strout.acme>
